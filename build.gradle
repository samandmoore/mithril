allprojects {
    project.group = "com.samandmoore"
    project.version = "0.0.1"
}

subprojects {
    ext {
        guavaVersion = "16.0"
        commonsEmailVersion = "1.3.2"
        commonsLangVersion = "3.3.1"
        slf4jVersion = "1.7.6"
        junitVersion = "4.11"
        mockitoVersion = "1.9.5"
    }

    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: "maven-publish"

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile "junit:junit:${junitVersion}"
        testCompile "org.mockito:mockito-all:${mockitoVersion}"
    }
}

project(":mithril-core") {
    dependencies {
        compile "com.google.guava:guava:${guavaVersion}"
        compile "org.apache.commons:commons-lang3:${commonsLangVersion}"
        compile "org.apache.commons:commons-email:${commonsEmailVersion}"

        compile "org.slf4j:slf4j-api:${slf4jVersion}"

        runtime "org.slf4j:jcl-over-slf4j:${slf4jVersion}"

        testCompile "org.slf4j:slf4j-simple:${slf4jVersion}"
    }

    publishing {
        publications {
            mithrilCore(MavenPublication) {
                from components.java
            }
        }
    }
}

project(":mithril-handlebars") {
    ext {
        handlebarsVersion = "1.2.1"
    }

    dependencies {
        compile project(":mithril-core")

        compile "com.github.jknack:handlebars:${handlebarsVersion}"
    }

    publishing {
        publications {
            mithrilHandlebars(MavenPublication) {
                from components.java
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.10"
}
